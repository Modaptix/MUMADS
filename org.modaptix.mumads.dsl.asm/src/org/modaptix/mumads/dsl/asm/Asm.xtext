grammar org.modaptix.mumads.dsl.asm.Asm 

with org.modaptix.xtext.expressions.ArithmeticExpression

hidden(SL_COMMENT) 

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.modaptix.org/xtext/expressions/ArithmeticExpression" as arithmeticExpression

generate asm "http://www.modaptix.org/mumads/dsl/asm/Asm"


Asm:
	(WS* EOL)*	
	(code+=Instruction (WS* EOL)+)+ 
;

Instruction:
	Import | MacroDefinition | InternalInstruction | ArchInstructionOrMacro
;

Import:
	'#'? ('import'|'include') WS* importURI=ID
;

MacroDefinition:
	name=ID WS ('MACRO'|'macro') (WS* EOL)+
	(code+=Instruction (WS* EOL)+)+
	WS ('ENDM'|'endm')
;

InternalInstruction:
	InternalInstructionEqu | InternalInstructionOrg | InternalInstructionDs
;

InternalInstructionEqu:
	name=ID WS ('equ'|'EQU') WS value = IntegerExpression
;

InternalInstructionOrg:
	WS ('org'|'ORG') WS address = IntegerExpression
;

InternalInstructionDs:
	name=ID WS ('ds'|'DS') ('.' variant=ID)? WS value = IntegerExpression
;

ArchInstructionOrMacro:
	(name=ID)? WS mnemonic=ID (WS operands+=Operand (COMMA operands+=Operand)* )?	
;

Operand:
	immediate ?= '#'?
	value = IntegerExpression
;

IntegerLiteralOrReference returns arithmeticExpression::IntegerExpression:
	IntegerLiteral | NamedReference
;

NamedReference:
	target=NamedReferenceName
;

NamedReferenceName returns ecore::EString:
	ID ( '[' ID ']' )?
;

terminal EOL: ('\r'? '\n')+;
terminal WS: (' '|'\t')+;
terminal COMMA: (',' WS?);
terminal ID: ('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;

terminal SL_COMMENT 	: ';' !('\n'|'\r')*;
