grammar org.modaptix.mumads.dsl.mpadl.Mpadl

with org.modaptix.xtext.expressions.ArithmeticExpression

hidden(SL_COMMENT, WS, EOL) 

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.modaptix.org/xtext/expressions/ArithmeticExpression" as arithmeticExpression

//generate mpadl "http://www.modaptix.org/mumads/dsl/mpadl/Mpadl"
import "http://www.modaptix.org/mumads/dsl/mpadl/Mpadl"

Mpadl:
	'architecture' name=STRING '{'
	
		'cpu_features' '{'
			cpuFeatures+=CpuFeature (',' cpuFeatures+=CpuFeature)*
		'}'
		
		'operand_types' '{'
			operandTypes+=OperandType (',' operandTypes+=OperandType)*
		'}'
		
		'interrupt_type'
			interruptType=INTERRUPT_TYPE
			
		'interrupt_modes' '{'
			interruptModes+=InterruptMode+
		'}'
		
		'instruction_set' '{'
			instructionSet+=(Instruction)+
		'}'
	'}'
;

CpuFeature:
	RegisterIndexable | RegisterIndexed | Flag
;

RegisterIndexable:
	RegisterReal | RegisterMmap	
;

RegisterReal:
	'real'
	name=ID
	'(' width=INT ')'	
;

RegisterMmap:
	'mmap'
	name=ID
	'(' width=INT ')'	
	address=HEX	
;

RegisterIndexed:
	RegisterRealIndexed | RegisterMmapIndexed	
;

RegisterRealIndexed:
	'real'
	name=ID
	'[' index=[RegisterIndexable] ']'
	'(' width=INT ')'	
;

RegisterMmapIndexed:
	'mmap'
	name=ID
	'[' index=[RegisterIndexable] ']'
	'(' width=INT ')'	
	address=HEX	
;

Flag:
	'flag'
	name=ID
;

OperandType:
	OperandTypeWithWidth | OperandTypeRegister
;

OperandTypeWithWidth:
	OperandTypeImmediate | OperandTypeAbsoluteAddress | OperandTypeRelativeAddress
;

OperandTypeImmediate:
	'immediate' longName=STRING name=ID width=INT
;

OperandTypeAbsoluteAddress:
	'absolute' longName=STRING name=ID width=INT
;

OperandTypeRelativeAddress:
	'relative' negNumType=NEGATIVE_NUMBER_TYPE longName=STRING name=ID width=INT 
;

OperandTypeRegister:
	'register' longName=STRING name=ID 
;

InterruptMode:
	name=ID
;

Instruction:
	(SimpleInstruction | ComplexInstruction) ';'
;

SimpleInstruction:
	name=ID '->' opcodes+=Opcode (',' opcodes+=Opcode)* 
;

Opcode returns arithmeticExpression::IntegerExpression:
	IntegerExpression
;

IntegerLiteralOrReference returns arithmeticExpression::IntegerExpression:
	IntegerLiteral | OperandReference
;

OperandReference:
	'[' operand=INT ']'
;

ComplexInstruction:
	name=ID
	variants+=InstructionVariant ('|' variants+=InstructionVariant)*
;

InstructionVariant:
	operands+=Operand (',' operands+=Operand)* '->' opcodes+=Opcode (',' opcodes+=Opcode)*
;

Operand:
	operandType=[NamedFeature]
;

enum INTERRUPT_TYPE: REAL='real' | WAIT='simulated';

enum NEGATIVE_NUMBER_TYPE: ONES_COMPLEMENT = '1s-complement' | TWOS_COMPLEMENT = '2s-complement';

terminal EOL: ('\r'? '\n')+;
terminal WS: (' '|'\t')+;

terminal STRING: 
	'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"' |
	"'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'"
; 

terminal SL_COMMENT: '//' !('\n'|'\r')*;
